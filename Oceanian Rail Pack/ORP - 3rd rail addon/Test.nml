switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_attach, vehicle_type_id){
    Item_ComengInterurbanMK2_VR    : return CB_RESULT_ATTACH_ALLOW  ;
    Item_ComengInterurbanMK2_VLine1    : return CB_RESULT_ATTACH_ALLOW  ;
    Item_ComengInterurbanMK2_Vline2    : return CB_RESULT_ATTACH_ALLOW  ;
    Item_MU_Trailer    : return CB_RESULT_ATTACH_ALLOW  ;
    return string(STR_NO_COUPLE_MU) ;
}


//ComengInterurbanMK2_VR
spriteset(spriteset_ComengInterurbanMK2_VR_purchase,   "gfx/ComengInterurbanMK2_VR.png") {template_purchase(7, 21)}
spriteset(spriteset_ComengInterurbanMK2_VR_Motor_lead,   "gfx/ComengInterurbanMK2_VR.png") {template_8(7, 39)} // <- Define a wagon, in this case the cab
spriteset(spriteset_ComengInterurbanMK2_VR_Motor_rear,   "gfx/ComengInterurbanMK2_VR.png") {template_8(7, 64)} // <- Define the cab wagon again, but this time using a slightly different alignment template to get it flipped. Unsure if necessary for PGT
spriteset(spriteset_ComengInterurbanMK2_VR_DrivingTrailer_lead,   "gfx/ComengInterurbanMK2_VR.png") {template_8(187, 39)}
spriteset(spriteset_ComengInterurbanMK2_VR_DrivingTrailer_rear,   "gfx/ComengInterurbanMK2_VR.png") {template_8(187, 64)}
spriteset(spriteset_ComengInterurbanMK2_VR_Trailer,   "gfx/ComengInterurbanMK2_VR.png") {template_8(7, 89)}
spriteset(spriteset_ComengInterurbanMK2_VR_NonControlMotor,   "gfx/ComengInterurbanMK2_VR.png") {template_8(187, 89)}
spriteset(spriteset_ComengInterurbanMK2_VR_NonControlMotor_r,   "gfx/ComengInterurbanMK2_VR.png") {template_8_r(187, 89)}

// C O D E
//Backward

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_2_r, position_in_vehid_chain % 3){
    2 : spriteset_ComengInterurbanMK2_VR_NonControlMotor_r;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}
switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_2r_r, position_in_vehid_chain_from_end % 3){
    2 : spriteset_ComengInterurbanMK2_VR_NonControlMotor;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_1_r, position_in_vehid_chain % 2){
    1 : spriteset_ComengInterurbanMK2_VR_NonControlMotor_r;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_0_r, position_in_vehid_chain % 2){
    1 : spriteset_ComengInterurbanMK2_VR_Trailer;
    return spriteset_ComengInterurbanMK2_VR_NonControlMotor;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_1r_r, position_in_vehid_chain_from_end % 2){
    1 : spriteset_ComengInterurbanMK2_VR_NonControlMotor;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_other_r, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : sw_ComengInterurbanMK2_VR_pantocheck_at_1_r;
    return sw_ComengInterurbanMK2_VR_pantocheck_at_1r_r;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_4_r, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : spriteset_ComengInterurbanMK2_VR_Trailer;
    return sw_ComengInterurbanMK2_VR_pantocheck_at_0_r;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_6_r, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : sw_ComengInterurbanMK2_VR_pantocheck_at_2_r;
    return sw_ComengInterurbanMK2_VR_pantocheck_at_2r_r;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_lencheck_r, num_vehs_in_vehid_chain ) {
    0 .. 2 : spriteset_ComengInterurbanMK2_VR_Trailer;
    3 : sw_ComengInterurbanMK2_VR_pantocheck_at_1_r;
    4 : sw_ComengInterurbanMK2_VR_sidecheck_4_r;
    5 : sw_ComengInterurbanMK2_VR_pantocheck_at_2_r;
    6 .. 7 : sw_ComengInterurbanMK2_VR_sidecheck_6_r;
    return sw_ComengInterurbanMK2_VR_sidecheck_other_r;
}

//Forward

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_2, position_in_vehid_chain % 3){
    2 : spriteset_ComengInterurbanMK2_VR_NonControlMotor_r;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}
switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_2r, position_in_vehid_chain_from_end % 3){
    2 : spriteset_ComengInterurbanMK2_VR_NonControlMotor;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_1, position_in_vehid_chain % 2){
    1 : spriteset_ComengInterurbanMK2_VR_NonControlMotor_r;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}
switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_pantocheck_at_1r, position_in_vehid_chain_from_end % 2){
    1 : spriteset_ComengInterurbanMK2_VR_NonControlMotor;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_other, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : sw_ComengInterurbanMK2_VR_pantocheck_at_1;
    return sw_ComengInterurbanMK2_VR_pantocheck_at_1r;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_4, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : sw_ComengInterurbanMK2_VR_pantocheck_at_1;
    return spriteset_ComengInterurbanMK2_VR_Trailer;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_sidecheck_6, num_vehs_in_vehid_chain/2 > position_in_vehid_chain ) {
    1 : sw_ComengInterurbanMK2_VR_pantocheck_at_2;
    return sw_ComengInterurbanMK2_VR_pantocheck_at_2r;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_lencheck, num_vehs_in_vehid_chain ) {
    0 .. 2 : spriteset_ComengInterurbanMK2_VR_Trailer;
    3 : sw_ComengInterurbanMK2_VR_pantocheck_at_1;
    4 : sw_ComengInterurbanMK2_VR_sidecheck_4;
    5 : sw_ComengInterurbanMK2_VR_pantocheck_at_2;
    6 .. 7 : sw_ComengInterurbanMK2_VR_sidecheck_6;
    return sw_ComengInterurbanMK2_VR_sidecheck_other;
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_panto_direction_r_pos, position_in_vehid_chain_from_end){
    0: spriteset_ComengInterurbanMK2_VR_Motor_lead; //Panto
    return spriteset_ComengInterurbanMK2_VR_DrivingTrailer_lead; //No Panto
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_panto_direction_r, vehicle_is_flipped){
    0 : sw_ComengInterurbanMK2_VR_panto_direction_r_pos;        //Front panto
    return spriteset_ComengInterurbanMK2_VR_Motor_rear;    //Rear panto
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_panto_flipcheck_r, vehicle_is_flipped){
    0 : sw_ComengInterurbanMK2_VR_panto_direction_r;        //Not flipped, give Panto
    return spriteset_ComengInterurbanMK2_VR_Motor_rear;    //Flipped, no panto
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_frontcheck_r, position_in_vehid_chain){
    0: sw_ComengInterurbanMK2_VR_panto_direction_r; //We need a pantograph, guaranteed
    return sw_ComengInterurbanMK2_VR_panto_flipcheck_r; //More Checks
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_panto_direction, vehicle_is_flipped){
    0 : spriteset_ComengInterurbanMK2_VR_Motor_lead;        //Front panto
    return spriteset_ComengInterurbanMK2_VR_Motor_rear;    //Rear panto
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_panto_flipcheck, vehicle_is_flipped){
    0 : sw_ComengInterurbanMK2_VR_panto_direction;        //Not flipped, give Panto
    return spriteset_ComengInterurbanMK2_VR_DrivingTrailer_rear;    //Flipped, no panto
}

switch(FEAT_TRAINS, SELF, sw_ComengInterurbanMK2_VR_frontcheck, position_in_vehid_chain){
    0: sw_ComengInterurbanMK2_VR_panto_direction; //We need a pantograph, guaranteed
    return sw_ComengInterurbanMK2_VR_panto_flipcheck; //More Checks
}

switch(FEAT_TRAINS, PARENT, sw_ComengInterurbanMK2_VR_directioncheck, vehicle_is_reversed){
    0: sw_ComengInterurbanMK2_VR_frontcheck; //We are going forwards
    return sw_ComengInterurbanMK2_VR_frontcheck_r; //We are going backwards
}

switch(FEAT_TRAINS, PARENT, sw_ComengInterurbanMK2_VR_revcheck, vehicle_is_reversed){
    0 : sw_ComengInterurbanMK2_VR_lencheck;
    return sw_ComengInterurbanMK2_VR_lencheck_r;
}

item(FEAT_TRAINS, Item_ComengInterurbanMK2_VR, 14801) {
    property {
        sprite_id:                        SPRITE_ID_NEW_TRAIN;
        refit_cost:                        0;
        climates_available:                ALL_CLIMATES;
        bitmask_vehicle_info:            0;
        misc_flags:                        bitmask(TRAIN_FLAG_MU,TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK); // <- Train Flag MU needed here

        refittable_cargo_classes:        CC_PASSENGERS;
        cargo_allow_refit:                [PASS, TOUR];

        track_type:                        ELRL;
        running_cost_base:                RUNNING_COST_ELECTRIC;
        engine_class:                     ENGINE_CLASS_ELECTRIC;
        ai_special_flag:                AI_FLAG_PASSENGER;
        dual_headed:                    1; // <- Double Headed, so cab on each end

        visual_effect_and_powered: VISUAL_EFFECT_ELECTRIC;

        name:                            string(STR_NAME_COMENGINTERURBANMK2);
        introduction_date:                date(1985,1,1);
        vehicle_life:                     50;
        model_life:                        30;
        length:                            8;

        weight:                        47 ton;
        speed:                            115 km/h;
        power:                            885 hp;
        tractive_effort_coefficient:     0.7;
        cost_factor:                     15;
        running_cost_factor:             190;
        cargo_capacity:                  127/param_capacity_mod;
        loading_speed: 45;

        variant_group: Item_ComengInterurbanMK2_VR;
        extra_flags:  bitmask (VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
    }
    graphics {
        default: sw_ComengInterurbanMK2_VR_directioncheck;
        can_attach_wagon: sw_ComengInterurbanMK2_attach ;
        purchase: spriteset_ComengInterurbanMK2_VR_purchase;
        additional_text: string(STR_INFO_COMENGINTERURBANMK2);
    }
    livery_override (Item_MU_Trailer){
        default                     : sw_ComengInterurbanMK2_VR_revcheck ;
        length                      : 8;
        cargo_capacity              : (127/param_capacity_mod) ;
        power                       : 442 ;
		weight:                        47;
		running_cost_factor:             48;
    }
}